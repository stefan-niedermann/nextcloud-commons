apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish' // needed for JitPack.io

android {
    compileSdk 34
    namespace 'it.niedermann.android.markdown'

    defaultConfig {
        minSdk 24
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    testOptions {
        unitTests {
            includeAndroidResources true
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.github.stefan-niedermann.nextcloud-commons'
                artifactId = 'markdown'
            }
        }
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'

    implementation project(':ocs')

    api "com.github.nextcloud:Android-SingleSignOn:$sso_version"
    implementation 'com.github.nextcloud:android-common:0.16.0'
    implementation 'com.github.stefan-niedermann:android-commons:1.0.0'

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.lifecycle:lifecycle-livedata:2.7.0'

    implementation 'org.jsoup:jsoup:1.17.2'

    def markwonVersion = '4.6.2'
    implementation "io.noties.markwon:core:$markwonVersion"
    implementation "io.noties.markwon:editor:$markwonVersion"
    implementation "io.noties.markwon:ext-strikethrough:$markwonVersion"
    implementation "io.noties.markwon:ext-tables:$markwonVersion"
    implementation "io.noties.markwon:ext-tasklist:$markwonVersion"
    implementation "io.noties.markwon:html:$markwonVersion"
    implementation "io.noties.markwon:image:$markwonVersion"
    implementation "io.noties.markwon:image-glide:$markwonVersion"
    implementation "io.noties.markwon:linkify:$markwonVersion"
    implementation "io.noties.markwon:simple-ext:$markwonVersion"
    implementation "io.noties.markwon:inline-parser:$markwonVersion"
    implementation("io.noties.markwon:syntax-highlight:$markwonVersion") {
        exclude group: 'org.jetbrains', module: 'annotations-java5'
    }
    implementation('io.noties:prism4j:2.0.0') {
        exclude group: 'org.jetbrains', module: 'annotations-java5'
    }
    annotationProcessor 'io.noties:prism4j-bundler:2.0.0'
    implementation 'org.jetbrains:annotations:24.1.0'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'

    testImplementation 'androidx.test:core:1.5.0'
    testImplementation 'androidx.arch.core:core-testing:2.2.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation "io.mockk:mockk:1.13.9"
    testImplementation 'org.robolectric:robolectric:4.11.1'
}