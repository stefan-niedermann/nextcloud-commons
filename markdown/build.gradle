apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish' // needed for JitPack.io

android {
    compileSdkVersion 32
    buildToolsVersion "31.0.0"

    defaultConfig {
        minSdkVersion 22
        targetSdkVersion 32
        versionCode 1006004
        versionName = '1.6.4'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    testOptions {
        unitTests {
            includeAndroidResources true
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.github.stefan-niedermann.nextcloud-commons'
                artifactId = 'markdown'
            }
        }
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.2'

    implementation "com.github.nextcloud:Android-SingleSignOn:$sso_version"
    implementation 'com.github.stefan-niedermann:android-commons:0.2.7'

    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.lifecycle:lifecycle-livedata:2.5.1'

    implementation 'org.jsoup:jsoup:1.15.3'

    def markwonVersion = '4.6.2'
    implementation "io.noties.markwon:core:$markwonVersion"
    implementation "io.noties.markwon:editor:$markwonVersion"
    implementation "io.noties.markwon:ext-strikethrough:$markwonVersion"
    implementation "io.noties.markwon:ext-tables:$markwonVersion"
    implementation "io.noties.markwon:ext-tasklist:$markwonVersion"
    implementation "io.noties.markwon:html:$markwonVersion"
    implementation "io.noties.markwon:image:$markwonVersion"
    implementation "io.noties.markwon:image-glide:$markwonVersion"
    implementation "io.noties.markwon:linkify:$markwonVersion"
    implementation "io.noties.markwon:simple-ext:$markwonVersion"
    implementation "io.noties.markwon:inline-parser:$markwonVersion"
    implementation("io.noties.markwon:syntax-highlight:$markwonVersion") {
        exclude group: 'org.jetbrains', module: 'annotations-java5'
    }
    implementation('io.noties:prism4j:2.0.0') {
        exclude group: 'org.jetbrains', module: 'annotations-java5'
    }
    annotationProcessor 'io.noties:prism4j-bundler:2.0.0'
    implementation 'org.jetbrains:annotations:23.0.0'

    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation "io.mockk:mockk:1.12.8"
    testImplementation 'org.robolectric:robolectric:4.8.2'
}